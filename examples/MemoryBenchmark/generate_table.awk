#!/usr/bin/gawk -f
#
# Usage: generate_table.sh < ${board}.txt
#
# Takes the file generated by collect.sh and generates an ASCII
# table that can be inserted into the README.md.

BEGIN {
  labels[0] = "baseline"
  labels[1] = "PlainDateTime"
  labels[2] = "ZonedDateTime"
  labels[3] = "TimeZone1"
  labels[4] = "TimeZone2"
  labels[5] = "ZoneRegistry (zonedb)"
  labels[6] = "ZoneAndLinkRegistry (zonedb)"
  labels[7] = "ZoneRegistry (zonedball)"
  labels[8] = "ZoneAndLinkRegistry (zonedball)"
  record_index = 0
}
{
  u[record_index]["flash"] = $2
  u[record_index]["ram"] = $4
  record_index++
}
END {
  NUM_ENTRIES = record_index

  base_flash = u[0]["flash"]
  base_ram = u[0]["ram"]
  for (i = 0; i < NUM_ENTRIES; i++) {
    if (u[i]["flash"] == -1) {
      u[i]["d_flash"] = -1
      u[i]["d_ram"] = -1
    } else {
      u[i]["d_flash"] = u[i]["flash"] - base_flash
      u[i]["d_ram"] = u[i]["ram"] - base_ram
    }
  }

  printf(\
    "+---------------------------------------------------------------------+\n")
  printf(\
    "| Functionality                         |  flash/  ram |        delta |\n")

  for (i = 0; i < NUM_ENTRIES; i++) {
    if (u[i]["flash"] == "-1") continue

    name = labels[i]
    if (name ~ /^baseline$/ \
        || name ~ /^PlainDateTime$/ \
        || name ~ /^ZoneRegistry/) {
      printf(\
    "|---------------------------------------+--------------+--------------|\n")
    }

    printf("| %-37s | %6d/%5d | %6d/%5d |\n",
        name, u[i]["flash"], u[i]["ram"], u[i]["d_flash"], u[i]["d_ram"])
  }
  printf(\
    "+---------------------------------------------------------------------+\n")
}
